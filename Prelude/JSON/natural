{- Create a JSON number from a Dhall `Natural`

```
let JSON = ./package.dhall
in  JSON.render (JSON.natural 42)
= "42"
```
-}
let JSON =
        ./Type sha256:88bc5a41073bad3d9d95cc2f9872482cdcd68de969a60069725bbfc240247c72
      ? ./Type

let natural
    : Natural → JSON
    =   λ(x : Natural)
      → λ(JSON : Type)
      → λ ( json
          : { string : Text → JSON
            , double : Double → JSON
            , integer : Integer → JSON
            , natural : Natural → JSON
            , object : List { mapKey : Text, mapValue : JSON } → JSON
            , array : List JSON → JSON
            , bool : Bool → JSON
            , null : JSON
            }
          )
      → json.natural x

in  natural
