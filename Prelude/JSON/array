{- Create a JSON array from a `List` of JSON values

```
let JSON = ./package.dhall
in  JSON.render (JSON.array [ JSON.double 1.0, JSON.bool True ])
= "[ 1.0, true ]"

let JSON/Type = ./Type
let JSON = ./package.dhall
in  JSON.render (JSON.array ([] : List JSON/Type))
= "[ ]"
```
-}
let JSON =
        ./Type sha256:88bc5a41073bad3d9d95cc2f9872482cdcd68de969a60069725bbfc240247c72
      ? ./Type

let List/map =
        ../List/map sha256:dd845ffb4568d40327f2a817eb42d1c6138b929ca758d50bc33112ef3c885680
      ? ../List/map

let array
    : List JSON → JSON
    =   λ(x : List JSON)
      → λ(JSON : Type)
      → λ ( json
          : { array : List JSON → JSON
            , bool : Bool → JSON
            , double : Double → JSON
            , integer : Integer → JSON
            , natural : Natural → JSON
            , null : JSON
            , object : List { mapKey : Text, mapValue : JSON } → JSON
            , string : Text → JSON
            }
          )
      → json.array (List/map JSON@1 JSON (λ(j : JSON@1) → j JSON json) x)

in  array
