{- Create a JSON number from a Dhall `Double`

```
let JSON = ./package.dhall
in  JSON.render (JSON.double 42.0)
= "42.0"

let JSON = ./package.dhall
in  JSON.render (JSON.double -1.5e-10)
= "-1.5e-10"
```
-}
let JSON =
        ./Type sha256:88bc5a41073bad3d9d95cc2f9872482cdcd68de969a60069725bbfc240247c72
      ? ./Type

let double
    : Double → JSON
    =   λ(x : Double)
      → λ(JSON : Type)
      → λ ( json
          : { string : Text → JSON
            , double : Double → JSON
            , integer : Integer → JSON
            , natural : Natural → JSON
            , object : List { mapKey : Text, mapValue : JSON } → JSON
            , array : List JSON → JSON
            , bool : Bool → JSON
            , null : JSON
            }
          )
      → json.double x

in  double
